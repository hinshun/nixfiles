{ inputs, config, lib, withSystem, ... }:
let
  mkTerranixConfig = system: let
    pkgs = withSystem system ({ pkgs, ... }: pkgs);
  in inputs.terranix.lib.terranixConfiguration {
    inherit system pkgs;
    extraArgs = {
      inherit (config.flake) nixosConfigurations;
    };
    modules = [
      ./terranix/options
      ./terranix/init.nix
      ./terranix/servers.nix
    ];
  };

  servers = import ./terranix/servers.nix {};
  colmenaInstances = lib.filterAttrs
    (_: inst: inst.colmena.enable or false)
    servers.compute.instances;

in {
  config = {
    flake.colmenaHive = inputs.colmena.lib.makeHive ({
      meta = {
        nixpkgs = withSystem "x86_64-linux" ({ pkgs, ... }: pkgs);
        specialArgs = {
          inherit inputs;
          inherit (config.flake) nixosModules;
        };
      };
    } // lib.mapAttrs (name: inst: { name, nodes, pkgs, ... }: {
      imports = inst.colmena.modules;
    }) colmenaInstances);

    perSystem = { pkgs, system, ... }:
    let
      colmena = inputs.colmena.packages.${system}.colmena;
      tf-config = mkTerranixConfig system;
    in {
      packages.tf-config = tf-config;

      apps = {
        plan = {
          type = "app";
          program = toString (pkgs.writers.writeBash "plan" ''
            tmpdir=$(mktemp -d)
            trap "rm -rf $tmpdir" EXIT
            ln -s ${tf-config} "$tmpdir/config.tf.json"
            export TF_DATA_DIR="$PWD/.terraform"
            ${pkgs.opentofu}/bin/tofu -chdir="$tmpdir" init -input=false
            ${pkgs.opentofu}/bin/tofu -chdir="$tmpdir" plan
          '');
        };

        apply = {
          type = "app";
          program = toString (pkgs.writers.writeBash "apply" ''
            set -e
            tmpdir=$(mktemp -d)
            trap "rm -rf $tmpdir" EXIT
            ln -s ${tf-config} "$tmpdir/config.tf.json"
            export TF_DATA_DIR="$PWD/.terraform"

            ${pkgs.opentofu}/bin/tofu -chdir="$tmpdir" init -input=false
            ${pkgs.opentofu}/bin/tofu -chdir="$tmpdir" apply

            mkdir -p ~/.ssh/config.d
            ips=$(${pkgs.opentofu}/bin/tofu -chdir="$tmpdir" output -json)
            echo "# Auto-generated by nix run .#apply" > ~/.ssh/config.d/infra
            echo "$ips" | ${pkgs.jq}/bin/jq -r 'to_entries[] | "Host \(.key)\n  HostName \(.value.value)\n  User root\n"' >> ~/.ssh/config.d/infra
            echo "Updated ~/.ssh/config.d/infra"

            ${colmena}/bin/colmena apply --impure --on @all
          '');
        };

        destroy = {
          type = "app";
          program = toString (pkgs.writers.writeBash "destroy" ''
            tmpdir=$(mktemp -d)
            trap "rm -rf $tmpdir" EXIT
            ln -s ${tf-config} "$tmpdir/config.tf.json"
            export TF_DATA_DIR="$PWD/.terraform"
            ${pkgs.opentofu}/bin/tofu -chdir="$tmpdir" init -input=false
            ${pkgs.opentofu}/bin/tofu -chdir="$tmpdir" destroy
          '');
        };

        update-ssh-config = {
          type = "app";
          program = toString (pkgs.writers.writeBash "update-ssh-config" ''
            set -e
            mkdir -p ~/.ssh/config.d
            tmpdir=$(mktemp -d)
            trap "rm -rf $tmpdir" EXIT
            ln -s ${tf-config} "$tmpdir/config.tf.json"
            export TF_DATA_DIR="$PWD/.terraform"
            ${pkgs.opentofu}/bin/tofu -chdir="$tmpdir" init -input=false -backend=true >/dev/null 2>&1
            ips=$(${pkgs.opentofu}/bin/tofu -chdir="$tmpdir" output -json)
            echo "# Auto-generated by nix run .#update-ssh-config" > ~/.ssh/config.d/infra
            echo "$ips" | ${pkgs.jq}/bin/jq -r 'to_entries[] | "Host \(.key)\n  HostName \(.value.value)\n  User root\n"' >> ~/.ssh/config.d/infra
            echo "Updated ~/.ssh/config.d/infra"
          '');
        };

        deploy = {
          type = "app";
          program = toString (pkgs.writers.writeBash "deploy" ''
            set -e
            ${colmena}/bin/colmena apply --impure "$@"
          '');
        };

        force-unlock = {
          type = "app";
          program = toString (pkgs.writers.writeBash "force-unlock" ''
            set -e
            if [ -z "$1" ]; then
              echo "Usage: nix run .#force-unlock <lock-id>"
              exit 1
            fi
            tmpdir=$(mktemp -d)
            trap "rm -rf $tmpdir" EXIT
            ln -s ${tf-config} "$tmpdir/config.tf.json"
            export TF_DATA_DIR="$PWD/.terraform"
            ${pkgs.opentofu}/bin/tofu -chdir="$tmpdir" init -input=false -backend=true >/dev/null 2>&1
            ${pkgs.opentofu}/bin/tofu -chdir="$tmpdir" force-unlock -force "$1"
          '');
        };
      };
    };
  };
}
